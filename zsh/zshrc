# ==============================================================================
# Plugins & Paths
# ==============================================================================

ZSH_PLUGIN_DIR="${ZSH_PLUGIN_DIR:-$HOME/.zsh/plugins}"
mkdir -p "$ZSH_PLUGIN_DIR"

# List of plugins to load
# Format: "github_user/repo"
plugins=(
  "romkatv/powerlevel10k"
  "zsh-users/zsh-autosuggestions"
)

# Clone plugins if missing and update fpath for completions
for plugin in $plugins; do
  plugin_name="${plugin#*/}"
  plugin_dir="$ZSH_PLUGIN_DIR/$plugin_name"

  if [[ ! -d "$plugin_dir" ]]; then
    echo "Cloning $plugin..."
    git clone --depth=1 "https://github.com/$plugin.git" "$plugin_dir"
  fi

  # Add completions to fpath before compinit
  if [[ -d "$plugin_dir/src" ]]; then
    fpath=("$plugin_dir/src" $fpath)
  fi
done

# Ensure unique entries in path arrays
typeset -gU cdpath fpath mailpath path

path=(
  $HOME/bin
  $HOME/go/bin
  $HOME/.cargo/bin
  $path
)

export EDITOR="nvim"
export BAT_PAGER="less -RXF"

# ==============================================================================
# Completion System
# ==============================================================================

# Completion Styles
zstyle ':completion:*:manuals' separate-sections true
zstyle ':completion:*:default' menu select=2
zstyle ':completion:*:options' description 'yes'
zstyle ':completion:*:options' auto-description '%d'
zstyle ':completion:*:descriptions' format '%F{yellow}-- %d --%f'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'
zstyle ':completion:*' verbose yes
zstyle ':completion:*' completer _expand _complete _match _prefix _approximate _list
zstyle ':completion:*:*files' ignored-patterns '*?.o' '*?~' '*\#'
zstyle ':completion:*' use-cache true
zstyle ':completion:*:*:-subscript-:*' tag-order indexes parameters
zstyle ':completion:*' list-separator '-->'

# Speed up compinit by using a cache file
autoload -Uz compinit
for dump in "${ZDOTDIR:-$HOME}/.zcompdump"(N.mh+24); do
  compinit
done
compinit -C

# ==============================================================================
# Shell Options
# ==============================================================================

bindkey -e

setopt BANG_HIST                 # Treat the '!' character specially during expansion.
setopt EXTENDED_HISTORY          # Write the history file in the ':start:elapsed;command' format.
setopt SHARE_HISTORY             # Share history between all sessions.
setopt HIST_EXPIRE_DUPS_FIRST    # Expire a duplicate event first when trimming history.
setopt HIST_IGNORE_DUPS          # Do not record an event that was just recorded again.
setopt HIST_IGNORE_ALL_DUPS      # Delete an old recorded event if a new event is a duplicate.
setopt HIST_FIND_NO_DUPS         # Do not display a previously found event.
setopt HIST_IGNORE_SPACE         # Do not record an event starting with a space.
setopt HIST_SAVE_NO_DUPS         # Do not write a duplicate event to the history file.
setopt HIST_VERIFY               # Do not execute immediately upon history expansion.
setopt HIST_BEEP                 # Beep when accessing non-existent history.
setopt COMPLETE_IN_WORD          # Complete from both ends of a word.
setopt ALWAYS_TO_END             # Move cursor to the end of a completed word.
setopt PATH_DIRS                 # Perform path search even on command names with slashes.
setopt AUTO_MENU                 # Show completion menu on a successive tab press.
setopt AUTO_LIST                 # Automatically list choices on ambiguous completion.
setopt AUTO_PARAM_SLASH          # If completed parameter is a directory, add a trailing slash.
unsetopt MENU_COMPLETE           # Do not autoselect the first completion entry.
unsetopt FLOW_CONTROL            # Disable start/stop characters in shell editor.

HISTFILE="${HISTFILE:-${ZDOTDIR:-$HOME}/.zhistory}"
HISTSIZE=1000000
SAVEHIST=1000000

WORDCHARS='*?_-.[]~&;!#$%^(){}<>'

# ==============================================================================
# Plugin Loading (Post-Completion)
# ==============================================================================

for plugin in $plugins; do
  plugin_name="${plugin#*/}"
  plugin_dir="$ZSH_PLUGIN_DIR/$plugin_name"

  # Source the plugin
  if [[ -f "$plugin_dir/$plugin_name.zsh" ]]; then
    source "$plugin_dir/$plugin_name.zsh"
  elif [[ -f "$plugin_dir/$plugin_name.zsh-theme" ]]; then
    source "$plugin_dir/$plugin_name.zsh-theme"
  elif [[ -f "$plugin_dir/$plugin_name.plugin.zsh" ]]; then
    source "$plugin_dir/$plugin_name.plugin.zsh"
  fi
done

function update-zsh-plugins {
  for plugin_dir in "$ZSH_PLUGIN_DIR"/*(N/); do
    echo "Updating ${plugin_dir:t}..."
    git -C "$plugin_dir" pull --rebase
  done
}

# ==============================================================================
# Aliases & Tools
# ==============================================================================

alias icat='kitten icat'
alias ip='ip -c=auto'
alias v='nvim +FF'
alias vi='/usr/bin/vim'
alias vim='nvim'
alias vimdiff='nvim -d'
alias docker-rm-dangling='docker rmi $(docker images -f "dangling=true" -q) -f'
alias kc='kubectl'
alias cdtmp='cd $(mktemp -d)'

# eza (better ls)
if (( $+commands[eza] )); then
  alias ls='eza --icons --group-directories-first'
  alias ll='ls -lh'
  alias la='ls -a'
  alias lla='ls -lha'
  alias lt='ls --tree'
else
  alias ls='ls --color=auto'
  alias ll='ls -lh'
  alias la='ls -A'
fi

# Pacman aliases
alias pacman-list-orphans='pacman -Qtdq'
alias pacman-pkg-info='pacman -Q -i'
alias pacx='sudo pacman --remove'
alias pacX='sudo pacman --remove --nosave --recursive'

# Kitty integration
if [[ "$TERM" == "xterm-kitty" ]]; then
  source ~/.zsh/kitty.zsh
fi

# Private settings
[[ -s ~/.zsh_private ]] && source ~/.zsh_private

# External tool initializations
eval "$(zoxide init zsh)"
eval "$(atuin init zsh --disable-up-arrow)"

# Powerlevel10k configuration
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh


